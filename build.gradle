plugins {
    id 'java'
    id 'io.typst.spigradle' version '3.1.2'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
    spigotmc()
}

dependencies {
    compileOnly spigot('1.16.5')
    compileOnly lombok()
    annotationProcessor lombok()
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

java {
    toolchain.setLanguageVersion(JavaLanguageVersion.of(11))
    withSourcesJar()
    withJavadocJar()
}

generateSpigotDescription {
    enabled = false
}

allprojects {
    group = 'io.typst'
    version = '1.1.1'
    def moduleName = it == rootProject ? "core" : name.substring(name.lastIndexOf('-') + 1)
    println(moduleName)
    ext.registerPublish = {
        publishing {
            publications {
                create(moduleName, MavenPublication) {
                    from(components['java'])
                    groupId = project.group
                    artifactId = project.name
                    version = project.version

                    pom {
                        name.set("${project.group}:${project.name}")
                        description.set("Normalized inventory operations for Bukkit.")
                        url.set("https://github.com/typst-io/bukkit-inventory")
                        licenses {
                            license {
                                name.set("The GNU General Public License, Version 3.0")
                                url.set("https://www.gnu.org/licenses/gpl-3.0.txt")
                            }
                        }
                        developers {
                            developer {
                                id.set("entrypointkr")
                                name.set("Junhyung Im")
                                email.set("entrypointkr@gmail.com")
                            }
                        }
                        scm {
                            connection.set("scm:git:git://github.com/typst-io/command.git")
                            developerConnection.set("scm:git:ssh://github.com:typst-io/command.git")
                            url.set("https://github.com/typst-io/command/tree/master")
                        }
                    }
                }
            }
            repositories {
                maven {
                    name = "sonatypeReleases"
                    url = uri("https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/")
                    credentials {
                        username = findProperty("ossrhUsername")?.toString()
                        password = findProperty("ossrhPassword")?.toString()
                    }
                }
            }
        }
        signing {
            sign(publishing.publications[moduleName])
        }
        java {
            withSourcesJar()
            withJavadocJar()
        }
        tasks.javadoc {
            options.encoding = 'UTF-8'
        }
        tasks.register("publishCentralPortal") {
            group = 'publishing'
            doLast {
                def url = new URL("https://ossrh-staging-api.central.sonatype.com/manual/upload/defaultRepository/$project.group")
                println(url)
                def con = url.openConnection() as HttpURLConnection
                def username = findProperty("ossrhUsername")?.toString()
                def password = findProperty("ossrhPassword")?.toString()
                def credential = Base64.encoder.encodeToString("$username:$password".getBytes())
                def authValue = "Bearer $credential"
                con.setRequestMethod("POST")
                con.setRequestProperty("Authorization", authValue)
                println(con.responseCode)
            }
        }
    }
}

registerPublish(this)
